-- Escreva uma consulta para obter o(s) nome(s) completo(s) do(s) piloto(s) (colunas forename e surname da tabela drivers separadas por um ' ') que já participaram de todas as corridas (tabela races) ocorridas em um circuito, seguido do nome do circuito (coluna name da tabela circuits). As colunas da tabela de saída devem ter os nomes "Piloto" e "Circuito", respectivamente.

t0 = circuits ⨝ races

-- Quantas corridas teve em cada circuito
t1 = π circuits.name, races.raceId (t0)
t2 = γ circuits.name; count(races.raceId)→TotalCorridasCircuito (t1)

-- Numero de corridas por circuito para cada piloto
t3 = circuits ⨝ races ⨝ results ⨝ drivers
t4 = π circuits.name, concat(drivers.forename, ' ', drivers.surname)→Piloto, races.raceId (t3)
t5 = γ circuits.name, Piloto; count(races.raceId)→NumCorridasCircuito (t4)

-- Resultado 
t6 = t2 ⨝ t5
t7 = σ NumCorridasCircuito = TotalCorridasCircuito (t6)
π Piloto, circuits.name→Circuito (t7)